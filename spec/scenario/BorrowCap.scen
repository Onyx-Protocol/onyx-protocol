
Test "Attempt to borrow over set cap ERC20"
    NewComptroller price:1.0
    NewOToken ZRX oZRX
    NewOToken BAT oBAT
    Comptroller SetMarketCaps (oBAT) (0.5e18) (0.5e18)
    Assert Equal (Comptroller BorrowCaps oBAT) (Exactly 0.5e18)
    Give oBAT 10e18 BAT -- Faucet some bat to borrow
    Support oZRX collateralFactor:0.5
    Support oBAT collateralFactor:0.5
    Prep Geoff Some ZRX oZRX
    Mint Geoff 100e18 oZRX
    EnterMarkets Geoff oZRX
    AllowFailures
    Borrow Geoff 1e18 oBAT
    Assert Revert
    Assert Equal (oToken oBAT BorrowBalance Geoff) (Exactly 0)
    Assert Equal (Erc20 BAT TokenBalance Geoff) (Exactly 0)
    Assert Equal (Erc20 BAT TokenBalance oBAT) (Exactly 10e18)

Test "Attempt to borrow at set cap ERC20"
    NewComptroller price:1.0
    NewOToken ZRX oZRX
    NewOToken BAT oBAT
    Comptroller SetMarketCaps (oBAT) (1000000000000000001) (1000000000000000001)
    Give oBAT 10e18 BAT -- Faucet some bat to borrow
    Support oZRX collateralFactor:0.5
    Support oBAT collateralFactor:0.5
    Prep Geoff Some ZRX oZRX
    Mint Geoff 100e18 oZRX
    EnterMarkets Geoff oZRX
    Borrow Geoff 1e18 oBAT
    Assert Equal (oToken oBAT BorrowBalance Geoff) (Exactly 1e18)
    Assert Equal (Erc20 BAT TokenBalance Geoff) (Exactly 1e18)
    Assert Equal (Erc20 BAT TokenBalance oBAT) (Exactly 9e18)
    Assert Equal (Comptroller MembershipLength Geoff) (Exactly 2)
    Assert True (Comptroller CheckMembership Geoff oZRX)
    Assert True (Comptroller CheckMembership Geoff oBAT)

Test "Attempt to borrow below set cap ERC20"
    NewComptroller price:1.0
    NewOToken ZRX oZRX
    NewOToken BAT oBAT
    Comptroller SetMarketCaps (oBAT) (10e18) (10e18)
    Give oBAT 10e18 BAT -- Faucet some bat to borrow
    Support oZRX collateralFactor:0.5
    Support oBAT collateralFactor:0.5
    Prep Geoff Some ZRX oZRX
    Mint Geoff 100e18 oZRX
    EnterMarkets Geoff oZRX
    Borrow Geoff 1e18 oBAT
    Assert Equal (oToken oBAT BorrowBalance Geoff) (Exactly 1e18)
    Assert Equal (Erc20 BAT TokenBalance Geoff) (Exactly 1e18)
    Assert Equal (Erc20 BAT TokenBalance oBAT) (Exactly 9e18)
    Assert Equal (Comptroller MembershipLength Geoff) (Exactly 2)
    Assert True (Comptroller CheckMembership Geoff oZRX)
    Assert True (Comptroller CheckMembership Geoff oBAT)

Test "Borrow some Eth over cap"
    NewComptroller price:1.0
    ListedOToken ZRX oZRX
    ListedEtherToken oETH initialExchangeRate:0.005e9
    SetCollateralFactor oZRX collateralFactor:0.5
    SetCollateralFactor oETH collateralFactor:0.5
    Comptroller SetMarketCaps (oETH) (0.0001e18) (0.0001e18)
    Donate oETH 0.003e18
    Prep Geoff Some ZRX oZRX
    Mint Geoff 1e18 oZRX
    EnterMarkets Geoff oZRX
    AllowFailures
    BorrowEth Geoff 0.001e18 oETH
    Assert Revert
    Assert Equal (EtherBalance oETH) 0.003e18

Test "Borrow some Eth enters Eth and succeeds when Eth not entered. At borrow cap"
    NewComptroller price:1.0
    ListedOToken ZRX oZRX
    ListedEtherToken oETH initialExchangeRate:0.005e9
    SetCollateralFactor oZRX collateralFactor:0.5
    SetCollateralFactor oETH collateralFactor:0.5
    Comptroller SetMarketCaps (oETH) (1000000000000001) (1000000000000001)
    Donate oETH 0.003e18
    Prep Geoff Some ZRX oZRX
    Mint Geoff 1e18 oZRX
    EnterMarkets Geoff oZRX
    Expect Changes (EtherBalance Geoff) +0.001e18
    BorrowEth Geoff 0.001e18 oETH
    Assert Equal (EtherBalance oETH) 0.002e18
    Assert Equal (Comptroller Liquidity Geoff) 4.99e17
    Assert Equal (Comptroller MembershipLength Geoff) (Exactly 2)
    Assert True (Comptroller CheckMembership Geoff oETH)

Test "Borrow some Eth enters Eth and succeeds when Eth not entered. At under borrow cap"
    NewComptroller price:1.0
    ListedOToken ZRX oZRX
    ListedEtherToken oETH initialExchangeRate:0.005e9
    SetCollateralFactor oZRX collateralFactor:0.5
    SetCollateralFactor oETH collateralFactor:0.5
    Comptroller SetMarketCaps (oETH) (0.01e18) (0.01e18)
    Donate oETH 0.003e18
    Prep Geoff Some ZRX oZRX
    Mint Geoff 1e18 oZRX
    EnterMarkets Geoff oZRX
    Expect Changes (EtherBalance Geoff) +0.001e18
    BorrowEth Geoff 0.001e18 oETH
    Assert Equal (EtherBalance oETH) 0.002e18
    Assert Equal (Comptroller Liquidity Geoff) 4.99e17
    Assert Equal (Comptroller MembershipLength Geoff) (Exactly 2)
    Assert True (Comptroller CheckMembership Geoff oETH)

Test "Setting borrow cap restricted to admin"
    NewComptroller price:1.0
    ListedOToken ZRX oZRX
    ListedEtherToken oETH initialExchangeRate:0.005e9
    SetCollateralFactor oZRX collateralFactor:0.5
    SetCollateralFactor oETH collateralFactor:0.5
    AllowFailures
    From Robert (Comptroller SetMarketCaps (oETH) (0.01e18) (0.01e18))
    Assert Revert

Test "Borrow cap guardian can set borrow caps"
    NewComptroller price:1.0
    ListedOToken ZRX oZRX
    ListedEtherToken oETH initialExchangeRate:0.005e9
    SetCollateralFactor oZRX collateralFactor:0.5
    SetCollateralFactor oETH collateralFactor:0.5
    Comptroller SetMarketCapGuardian Geoff
    From Geoff (Comptroller SetMarketCaps (oZRX) (0.5e18) (0.5e18))
    AllowFailures
    From Robert (Comptroller SetMarketCaps (oZRX) (0.01e18) (0.01e18)) -- Robert still can't...
    Assert Revert
    From Robert (Comptroller SetMarketCaps (oZRX) (0.01e18) (0.01e18))
    Assert Revert
    Assert Equal (Comptroller BorrowCaps oZRX) (Exactly 0.5e18)
    Assert Equal (Comptroller MarketCapGuardian) (User Geoff Address)

Test "Only admin can set Borrow Cap Guardian"
    NewComptroller price:1.0
    AllowFailures
    From Robert (Comptroller SetMarketCapGuardian Robert) -- Robert has really gone rogue
    Assert Revert

Test "SetBorrowCaps works correctly too"
    NewComptroller price:1.0
    NewOToken ZRX oZRX
    NewOToken BAT oBAT
    NewOToken USDC oUSDC
    Comptroller SetMarketCaps (oBAT oUSDC) (0.5e18 1000001) (0.5e18 1000001)
    Assert Equal (Comptroller BorrowCaps oBAT) (Exactly 0.5e18)
    Assert Equal (Comptroller BorrowCaps oUSDC) (Exactly 1000001)
    Give oBAT 10e18 BAT -- Faucet some bat to borrow
    Give oUSDC 20e6 USDC
    Support oZRX collateralFactor:0.5
    Support oBAT collateralFactor:0.5
    Support oUSDC collateralFactor:0.5
    Prep Geoff Some ZRX oZRX
    Mint Geoff 100e18 oZRX
    EnterMarkets Geoff oZRX
    AllowFailures
    Borrow Geoff 1e18 oBAT
    Assert Revert
    Borrow Geoff 2e6 oUSDC
    Assert Revert
    Successfully
    Borrow Geoff 1e6 oUSDC
    Assert Equal (oToken oBAT BorrowBalance Geoff) (Exactly 0)
    Assert Equal (Erc20 BAT TokenBalance Geoff) (Exactly 0)
    Assert Equal (Erc20 BAT TokenBalance oBAT) (Exactly 10e18)
    Assert Equal (Erc20 USDC TokenBalance Geoff) (Exactly 1e6)
