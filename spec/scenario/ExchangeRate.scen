
Test "Initial Exchange Rate"
    NewComptroller
    NewOToken ZRX oZRX initialExchangeRate:2e9
    Assert Equal (OToken oZRX ExchangeRateStored) (Exp 2e9)

Test "Initial Exchange Rate with Mint"
    NewComptroller
    ListedOToken ZRX oZRX initialExchangeRate:5e9
    -- Check initial exchange holds
    Invariant Remains (OToken oZRX ExchangeRateStored) (Exp 5e9)
    -- Mint some tokens and verify invariant still holds
    Prep Geoff Some ZRX oZRX
    Mint Geoff 50e18 oZRX
    -- Also, verify token was minted at given exchange rate
    Assert Equal (Erc20 oZRX TotalSupply) (Exactly 10e9)
    Assert Equal (Erc20 oZRX TokenBalance Geoff) (Exactly 10e9)
    -- Let's mint again and verify it's still good.
    Prep Torrey Some ZRX oZRX
    Mint Torrey 50e18 oZRX
    -- Also, verify token was minted at given exchange rate
    Assert Equal (Erc20 oZRX TotalSupply) (Exactly 20e9)
    Assert Equal (Erc20 oZRX TokenBalance Torrey) (Exactly 10e9)

Test "ZRX: Exch. Rate:2e9, Cash(51e18) + Borrows(2.0e18) - Reserves(0.5e18) / Tokens(2.5e10)"
    NewComptroller
    -- Decimals You=18, Decimals Us=8 -> 2e9 Exchange Rate
    ListedOToken ZRX oZRX initialExchangeRate:2e9 decimals:8
    Assert Equal (OToken oZRX ExchangeRateStored) (Exp 2e9)
    -- Mint 50.0 ZRX at given exchange rate
    Prep Geoff Some ZRX oZRX
    Assert Equal (Erc20 oZRX TotalSupply) Zero
    Mint Geoff 50e18 oZRX
    -- 50e18 / 2e9 = 2.5e10
    Assert Equal (Erc20 oZRX TotalSupply) (Exactly 2.5e10)
    Assert Equal (Erc20 oZRX TokenBalance Geoff) (Exactly 2.5e10)
    -- Set cash
    Erc20 ZRX Faucet oZRX 1.0e18
    Assert Equal (Erc20 ZRX TokenBalance oZRX) (Exactly 51.0e18)
    -- Mock total borrows
    OToken oZRX Mock totalBorrows 2.0e18
    Assert Equal (OToken oZRX TotalBorrows) (Exactly 2.0e18)
    -- Mock total reserves
    OToken oZRX Mock totalReserves 0.5e18
    Assert Equal (OToken oZRX Reserves) (Exactly 0.5e18)
    -- Okay, we're all set, let's check the exchange rate
    -- (51+2-0.5)e18/2.5e10 = 52.5e18/2.5e10 = 21e8 -> 21e26 (Exp)
    Assert Equal (OToken oZRX ExchangeRateStored) (Exp 2.1e9)

Test "USDC: Exch. Rate:2e-3, Cash(51e18) + Borrows(2.0e18) - Reserves(0.5e18) / Tokens(2.5e10)"
    NewComptroller
    -- Decimals You=6, Decimals Us=8 -> 2e-3 Exchange Rate
    ListedOToken USDC oUSDC initialExchangeRate:2e-3 decimals:8
    Assert Equal (OToken oUSDC ExchangeRateStored) (Exp 2e-3)
    -- Mint 50.0 USDC at given exchange rate
    Prep Geoff Little USDC oUSDC
    Assert Equal (Erc20 oUSDC TotalSupply) (Exactly 0.0)
    Mint Geoff 50e6 oUSDC
    -- 5.0e7 / 2e-3 = 2.5e10
    Assert Equal (Erc20 oUSDC TotalSupply) (Exactly 2.5e10)
    Assert Equal (Erc20 oUSDC TokenBalance Geoff) (Exactly 2.5e10)
    -- Set cash
    Erc20 USDC Faucet oUSDC 2.0e6
    Assert Equal (Erc20 USDC TokenBalance oUSDC) (Exactly 52.0e6)
    -- Mock total borrows
    OToken oUSDC Mock totalBorrows 5.0e6
    Assert Equal (OToken oUSDC TotalBorrows) (Exactly 5.0e6)
    -- Mock total reserves
    OToken oUSDC Mock totalReserves 1.5e6
    Assert Equal (OToken oUSDC Reserves) (Exactly 1.5e6)
    -- Okay, we're all set, let's check the exchange rate
    -- (52+5-1.5)e6/2.5e10 = 55.5e6/2.5e10 = 2.22e-3 -> 2.22e15 (Exp)
    Assert Equal (OToken oUSDC ExchangeRateStored) (Exp 2.22e-3)
