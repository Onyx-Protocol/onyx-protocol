-- Tests for the grants and math patch

Macro DeployComptroller price=1.0
    Unitroller Deploy
    PriceOracle Deploy Fixed price
    PriceOracleProxy Deploy Admin (PriceOracle Address) (Address Zero) (Address Zero) (Address Zero) (Address Zero) (Address Zero)
    ComptrollerImpl Deploy Scenario ComptrollerScen
    Unitroller SetPendingImpl ComptrollerScen
    ComptrollerImpl ComptrollerScen Become
    Comptroller SetPriceOracle (PriceOracleProxy Address)
    Comptroller SetCloseFactor 0.5
    Comptroller LiquidationIncentive 1.1

Macro SetupMarkets
    NewOToken ZRX oZRX
    NewOToken BAT oBAT
    Support oZRX collateralFactor:0.5
    Support oBAT collateralFactor:0.5

Macro SetupXcnToken xcnInitAmount=5000000e18
    Erc20 Deploy Standard XCN "XCN Token" 18
    Give (Address Comptroller) xcnInitAmount XCN
    Comptroller Send "setXcnAddress(address)" (Address XCN)

-- NewComptroller, but with markets listed so that we can make them xcn markets in constructor
Macro FlywheelComptroller price=1.0 borrowRate=0.000005 xcnInitAmount=5000000e18
    DeployComptroller price
    SetupMarkets
    SetupXcnToken xcnInitAmount

Macro InitUsage
    Prep Geoff 100e18 ZRX oZRX
    Mint Geoff 50e18 oZRX--tokenbalance = 50e18 / 2e9 = 2.5e10
    Prep Coburn Some BAT oBAT
    Mint Coburn 6e18 oBAT--tokenbalance = 6e18 / 2e9 = 3e9
    EnterMarkets Coburn oBAT
    Borrow Coburn 1e18 oZRX

Macro InitUsageAndSpeeds
    InitUsage
    Comptroller SetXcnSpeeds (oZRX oBAT) (1 1) (1 1)

Test "XCN can be granted in combination with liquidity rewards"
    FlywheelComptroller
    InitUsageAndSpeeds
    Assert Equal (Comptroller XcnAccrued Geoff) 0
    Assert Equal (Erc20 XCN TokenBalance Geoff) 0
    FastForward 1000 Blocks
    Comptroller ClaimXcn Geoff
    Comptroller Send "_grantXcn(address,uint256)" (Address Geoff) 1000
    Assert Equal (Comptroller XcnAccrued Geoff) 0
    Assert Equal (Erc20 XCN TokenBalance Geoff) 2000 -- 1000 (grant) + 1000 (XCN supply rewards)

Test "XCN can be granted"
    -- Can be granted once
    FlywheelComptroller
    InitUsageAndSpeeds
    Assert Equal (Comptroller XcnAccrued Geoff) 0
    Assert Equal (Erc20 XCN TokenBalance Geoff) 0
    Comptroller Send "_grantXcn(address,uint256)" (Address Geoff) 1000
    Assert Equal (Comptroller XcnAccrued Geoff) 0
    Assert Equal (Erc20 XCN TokenBalance Geoff) 1000
    -- Assert Log XcnGranted (recipient (Address Geoff)) (amount "1000")
    -- Can be granted multiple times
    Comptroller Send "_grantXcn(address,uint256)" (Address Geoff) 2000
    Assert Equal (Comptroller XcnAccrued Geoff) 0
    Assert Equal (Erc20 XCN TokenBalance Geoff) 3000

Test "XCN can be streamed to contributors"
    FlywheelComptroller
    InitUsageAndSpeeds
    Assert Equal (Comptroller XcnAccrued Torrey) 0
    Assert Equal (Erc20 XCN TokenBalance Torrey) 0
    Comptroller Send "_setContributorXcnSpeed(address,uint256)" (Address Torrey) 300
    -- Assert Log ContributorXcnSpeedUpdated (recipient (Address Torrey)) (amount "300")
    FastForward 1000 Blocks
    -- Just claimXcn does not receive XCN
    Comptroller ClaimXcn Torrey
    Assert Equal (Comptroller XcnAccrued Torrey) 0
    Assert Equal (Erc20 XCN TokenBalance Torrey) 0
    -- Calling updateContributorRewards and then claimXcn receives XCN
    Comptroller UpdateContributorRewards Torrey
    Assert Equal (Comptroller XcnAccrued Torrey) 300000
    Comptroller ClaimXcn Torrey
    Assert Equal (Comptroller XcnAccrued Torrey) 0
    Assert Equal (Erc20 XCN TokenBalance Torrey) 300000

Test "XCN can be streamed in combination with liquidity rewards"
    FlywheelComptroller
    InitUsageAndSpeeds
    Comptroller Send "_setContributorXcnSpeed(address,uint256)" (Address Geoff) 300
    FastForward 1000 Blocks
    -- Just claimXcn does not receive XCN
    Comptroller UpdateContributorRewards Geoff
    Assert Equal (Comptroller XcnAccrued Geoff) 300000
    Comptroller ClaimXcn Geoff
    Assert Equal (Comptroller XcnAccrued Geoff) 0
    Assert Equal (Erc20 XCN TokenBalance Geoff) 301000 -- 300000 (contributer grant) + 1000 (XCN supply rewards)

Test "XCN streaming can be changed for contributors"
    FlywheelComptroller
    InitUsageAndSpeeds
    Comptroller Send "_setContributorXcnSpeed(address,uint256)" (Address Torrey) 300
    FastForward 1000 Blocks
    Comptroller Send "_setContributorXcnSpeed(address,uint256)" (Address Torrey) 600
    FastForward 1000 Blocks
    Comptroller UpdateContributorRewards Torrey
    Comptroller ClaimXcn Torrey
    Assert Equal (Comptroller XcnAccrued Torrey) 0
    Assert Equal (Erc20 XCN TokenBalance Torrey) 900000
    Comptroller Send "_setContributorXcnSpeed(address,uint256)" (Address Torrey) 0
    FastForward 2000 Blocks
    Assert Equal (Comptroller XcnAccrued Torrey) 0
    Assert Equal (Erc20 XCN TokenBalance Torrey) 900000
