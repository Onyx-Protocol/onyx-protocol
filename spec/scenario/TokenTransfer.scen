
Test "Simple oToken Transfer"
    NewComptroller
    ListedOToken ZRX oZRX initialExchangeRate:1e9
    Prep Geoff Some ZRX oZRX
    Mint Geoff 50e18 oZRX
    -- Just to be sure, check initial balances
    Assert Equal (Erc20 oZRX TokenBalance Geoff) (Exactly 50e9)
    Assert Equal (Erc20 oZRX TokenBalance Torrey) Zero
    -- Just transfer
    Transfer Geoff Torrey 10e9 oZRX
    Assert Equal (Erc20 oZRX TokenBalance Geoff) (Exactly 40e9)
    Assert Equal (Erc20 oZRX TokenBalance Torrey) (Exactly 10e9)

Test "Simple oToken Transfer When Underlying Paused"
    NewComptroller
    ListedOToken WBTC oWBTC initialExchangeRate:0.1 tokenType:WBTC
    Prep Geoff Some WBTC oWBTC
    Mint Geoff 50e8 oWBTC
    -- Just to be sure, check initial balances
    Assert Equal (Erc20 oWBTC TokenBalance Geoff) (Exactly 50e9)
    Assert Equal (Erc20 oWBTC TokenBalance Torrey) Zero
    -- Just transfer
    Erc20 WBTC Pause
    Transfer Geoff Torrey 10e9 oWBTC
    Assert Equal (Erc20 oWBTC TokenBalance Geoff) (Exactly 40e9)
    Assert Equal (Erc20 oWBTC TokenBalance Torrey) (Exactly 10e9)

Test "Simple oToken Transfer 1:1 Rate"
    NewComptroller
    ListedOToken ZRX oZRX initialExchangeRate:1e0
    Prep Geoff Some ZRX oZRX
    Mint Geoff 50e18 oZRX
    -- Just to be sure, check initial balances
    Assert Equal (Erc20 oZRX TokenBalance Geoff) (Exactly 50e18)
    Assert Equal (Erc20 oZRX TokenBalance Torrey) Zero
    -- Just transfer
    Transfer Geoff Torrey 10e18 oZRX
    Assert Equal (Erc20 oZRX TokenBalance Geoff) (Exactly 40e18)
    Assert Equal (Erc20 oZRX TokenBalance Torrey) (Exactly 10e18)

Test "Simple oToken Transfer Not Allowed by Comptroller"
    NewComptroller
    ListedOToken ZRX oZRX initialExchangeRate:1e0
    Comptroller SetCollateralFactor oZRX 0.1
    EnterMarkets Geoff oZRX
    Prep Geoff Some ZRX oZRX
    Mint Geoff 50e18 oZRX
    -- Just to be sure, check initial balances
    Invariant Remains (Erc20 oZRX TokenBalance Geoff) (Exactly 50e18)
    Invariant Remains (Erc20 oZRX TokenBalance Torrey) Zero
    -- Just transfer
    AllowFailures
    Transfer Geoff Torrey 60e18 oZRX
    Assert Failure COMPTROLLER_REJECTION TRANSFER_COMPTROLLER_REJECTION INSUFFICIENT_LIQUIDITY

Test "Simple oToken Transfer From"
    NewComptroller
    ListedOToken ZRX oZRX initialExchangeRate:1e9
    Prep Geoff Some ZRX oZRX
    Mint Geoff 50e18 oZRX
    -- Just to be sure, check initial balances
    Invariant Remains (Erc20 oZRX TokenBalance Torrey) Zero
    Assert Equal (Erc20 oZRX TokenBalance Geoff) (Exactly 50e9)
    Assert Equal (Erc20 oZRX TokenBalance Coburn) Zero
    -- Add approval
    From Geoff (Erc20 oZRX Approve Torrey 25e9)
    Assert Equal (Erc20 oZRX Allowance Geoff Torrey) (Exactly 25e9)
    -- Perform transfer from
    From Torrey (Erc20 oZRX TransferFrom Geoff Coburn 10e9)
    Assert Equal (Erc20 oZRX TokenBalance Geoff) (Exactly 40e9)
    Assert Equal (Erc20 oZRX TokenBalance Coburn) (Exactly 10e9)
    Assert Equal (Erc20 oZRX Allowance Geoff Torrey) (Exactly 15e9)

Test "oToken Transfer From Not Allowed"
    NewComptroller
    ListedOToken ZRX oZRX initialExchangeRate:1e9
    Prep Geoff Some ZRX oZRX
    Mint Geoff 50e18 oZRX
    --
    Invariant Remains (Erc20 oZRX TokenBalance Geoff) (Exactly 50e9)
    Invariant Remains (Erc20 oZRX TokenBalance Torrey) (Exactly 0e9)
    AllowFailures
    Erc20 oZRX TransferFrom Geoff Torrey 10e9
    Assert Failure MATH_ERROR TRANSFER_NOT_ALLOWED

Test "oToken Transfer paused"
    NewComptroller
    ListedOToken ZRX oZRX initialExchangeRate:1e9
    Prep Geoff Some ZRX oZRX
    Mint Geoff 50e18 oZRX
    -- Just to be sure, check initial balances
    Assert Equal (Erc20 oZRX TokenBalance Geoff) (Exactly 50e9)
    Assert Equal (Erc20 oZRX TokenBalance Coburn) Zero
    -- Pause and attempt transfer
    Comptroller SetPauseGuardian Coburn
    From Coburn (Comptroller SetGuardianPaused "Transfer" True)
    AllowFailures
    Transfer Geoff Torrey 10e9 oZRX
    Assert Revert "revert transfer is paused"
    Assert Equal (Erc20 oZRX TokenBalance Geoff) (Exactly 50e9)
    Assert Equal (Erc20 oZRX TokenBalance Coburn) Zero
    -- unPause and complete transfer
    Invariant Success
    Comptroller SetGuardianPaused "Transfer" False
    Transfer Geoff Coburn 10e9 oZRX
    Assert Equal (Erc20 oZRX TokenBalance Geoff) (Exactly 40e9)
    Assert Equal (Erc20 oZRX TokenBalance Coburn) (Exactly 10e9)
